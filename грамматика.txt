statements  : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement		: KEYWORD:ВЕРНИСЬ-КА expr?
						: KEYWORD:ПРОДОЛЖАЙ
						: KEYWORD:ТОРМОЗИ
						: expr

expr        : KEYWORD:ВЪ IDENTIFIER EQ expr
            : comp-expr ((KEYWORD:ДА|KEYWORD:ЛИБО) comp-expr)*

comp-expr   : KEYWORD:НЕ comp-expr
            : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr  :	term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            : power

power       : call (POW factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : INT|FLOAT|STRING|IDENTIFIER
            : LPAREN expr RPAREN
            : list-expr
            : if-expr
            : for-expr
            : while-expr
            : func-def

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : KEYWORD:ЕСЛИ expr KEYWORD:ПОСЛЕ
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ|if-expr-b|if-expr-c)

if-expr-b   : KEYWORD:КОЛИ expr KEYWORD:ПОСЛЕ
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ|if-expr-b|if-expr-c)

if-expr-c   : KEYWORD:ИЛИ
              statement
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ)

for-expr    : KEYWORD:ПО IDENTIFIER EQ expr KEYWORD:ДО expr 
              (KEYWORD:ШАГ expr)? KEYWORD:ПОСЛЕ
              statement
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ)

while-expr  : KEYWORD:ПОКА expr KEYWORD:ПОСЛЕ
              statement
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ)

func-def    : KEYWORD:ВЕСЕЛЬЕ IDENTIFIER?
              LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
              (ARROW expr)
            | (NEWLINE statements KEYWORD:ЗАКОНЧИТЬ)